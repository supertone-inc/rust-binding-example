LIB_NAME := "hello_emscripten"

LIB_FILE_NAME := if os() == "windows" {
    LIB_NAME + ".lib"
} else {
    "lib" + LIB_NAME + ".a"
}

export CARGO_BUILD_TARGET := "wasm32-unknown-emscripten"

TARGET_DIR := "../../target/" + CARGO_BUILD_TARGET + "/debug"

EMSDK_DIR := absolute_path("build/_deps/emsdk-src")

build:
    #!/usr/bin/env bash
    set -e

    cmake \
        -S . \
        -B build \
        -D HELLO_INCLUDE_DIR="{{absolute_path("include")}}" \
        -D HELLO_LIB_DIR="{{absolute_path("lib")}}"

    rustup target add ${CARGO_BUILD_TARGET}
    source {{EMSDK_DIR}}/emsdk_env.sh
    cargo build
    mkdir -p lib
    cp {{TARGET_DIR}}/{{LIB_FILE_NAME}} lib/{{LIB_FILE_NAME}}

    cmake --build build
    cmake --install build --prefix hello

test: build
    #!/usr/bin/env bash
    set -e

    cd tests
    npm install
    npm test

test-quick:
    #!/usr/bin/env bash
    set -e

    cd tests
    npm test
