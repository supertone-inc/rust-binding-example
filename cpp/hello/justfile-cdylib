LIB_NAME := "hello_cpp"

LIB_FILE_NAME := if os() == "windows" {
    LIB_NAME + ".dll"
} else if os() == "macos" {
    "lib" + LIB_NAME + ".dylib"
} else {
    "lib" + LIB_NAME + ".so"
}

TARGET_DIR := "../../target/debug"

build:
    cargo rustc --crate-type cdylib
    mkdir -p hello/lib
    cp {{TARGET_DIR}}/{{LIB_FILE_NAME}} hello/lib/{{LIB_FILE_NAME}}

    if [ {{os()}} = "windows" ]; then \
        cp {{TARGET_DIR}}/{{LIB_FILE_NAME}}.lib hello/lib/{{LIB_FILE_NAME}}.lib; \
    fi

test: build
    cmake \
        -S tests \
        -B tests/build \
        -D HELLO_INCLUDE_DIR="{{absolute_path("hello/include")}}" \
        -D HELLO_LIB_DIR="{{absolute_path("hello/lib")}}" \
        -D HELLO_STATIC=OFF
    cmake --build tests/build

    if [ {{os()}} = "windows" ]; then \
        tests/build/Debug/tests; \
    else \
        tests/build/tests; \
    fi
